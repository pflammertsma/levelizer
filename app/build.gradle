buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'android-apt'

repositories {
    maven { url "http://dl.bintray.com/pixplicity/maven" }
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        debug {
            Properties props = new Properties()
            try {
                props.load(new FileInputStream(".signing/debug.properties"))
                storeFile file('../.signing/debug.jks')
                storePassword(props['DEBUG_STORE_PASSWORD'])
                keyAlias(props['DEBUG_KEY_ALIAS'])
                keyPassword(props['DEBUG_KEY_PASSWORD'])
            } catch (IOException e) {
                // Ignore
            }
        }
        release {
            Properties props = new Properties()
            try {
                props.load(new FileInputStream(".signing/release.properties"))
                storeFile file('../.signing/release.jks')
                storePassword(props['RELEASE_STORE_PASSWORD'])
                keyAlias(props['RELEASE_KEY_ALIAS'])
                keyPassword(props['RELEASE_KEY_PASSWORD'])
            } catch (IOException e) {
                // Ignore
            }
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.0"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24

        applicationId "org.dutchaug.levelizer"
        versionCode 6
        versionName "1.1.0"

        vectorDrawables.useSupportLibrary = true

        buildConfigField("boolean", "DEBUG_SHOW_TOASTS", "false")
        buildConfigField("boolean", "DEBUG_LOG", "false")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile('com.crashlytics.sdk.android:crashlytics:2.6.0@aar') {
        transitive = true;
    }

    compile 'com.jakewharton:butterknife:8.2.0'
    apt 'com.jakewharton:butterknife-compiler:8.2.0'

    compile 'org.adw.library:discrete-seekbar:1.0.1'

    compile 'com.pixplicity.easyprefs:library:1.8.1@aar'

    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
}
